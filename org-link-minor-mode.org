#+TITLE: org-link-minor-mode
#+SETUPFILE: ~/org/setup2.org
#+PROPERTY: eval never
#+COMMENT: comments noweb
#+LINK: elisp http://www.gnu.org/software/emacs/manual/html_node/elisp/%s.html

* Header
:PROPERTIES:
:ID:       14f6023b-6e29-4266-82f2-d227902d4cf7
:END:

#+name: header-comments
#+begin_src emacs-lisp
;;; org-link-minor-mode.el -- Enable org-mode bracket links in non-org modes
;;
;; Copyright (C) 2012
;; Author: Sean O'Halpin <sean dot ohalpin at gmail dot com>
;;
;; Enables org-mode bracket links of the form:
;;
;;   [[http://www.bbc.co.uk][BBC]]
;;   [[org-link-minor-mode]]
;;
;; Note that =org-toggle-link-display= will also work when this mode
;; is enabled.
;;
#+end_src

* Require org-mode
:PROPERTIES:
:ID:       0e3b4960-cc9f-4ebe-9acf-c6632b3c68f2
:END:

As we rely on many =org-mode= functions, to avoid warnings, we
load =org-mode=:

#+name: requires
#+begin_src emacs-lisp
(require 'org)
#+end_src

* Using the =define-minor-mode= macro
:PROPERTIES:
:ID:       2e643e2a-acb4-43dd-92b3-d8048f66f854
:END:

The simplest way to define a new minor mode is to use
the [[elisp:Defining-Minor-Modes][=define-minor-mode=]] macro:

#+name: define-minor-mode
#+begin_src emacs-lisp :noweb tangle
  (define-minor-mode org-link-minor-mode
    "Toggle display of org-mode style bracket links in non-org-mode buffers."
    :lighter " org-link"

    «body»
    )
#+end_src

The =:lighter= keyword parameter defines what appears in the mode line.

* The body
:PROPERTIES:
:ID:       4e5b0abe-bc41-43f6-9271-b6365a7b5bce
:END:

We start the body by setting up the font lock keywords, using
org-mode's =org-activate-bracket-links= to do the heavy lifting. We then
branch depending on whether we're entering or exiting the mode:

#+name: body
#+begin_src emacs-lisp :noweb tangle
  (let ((org-link-minor-mode-keywords
         (list
          '(org-activate-plain-links)
          '(org-activate-bracket-links (0 'org-link t))
          ))
        (org-link-minor-mode-link-regexp
         (concat "\(" org-plain-link-re "\)\|\(" org-bracket-link-regexp "\)" ))
        )
    (if org-link-minor-mode
        «enter-minor-mode»
      «exit-minor-mode»
      )
    )
#+end_src

* Entering the minor mode
:PROPERTIES:
:ID:       a1b76a00-4444-4b5c-bbfa-54c88dac769f
:END:

If we're already in org-mode, display a message and
switch =org-link-minor-mode= off. We need to do it this way as by this
point we've already entered the mode.

#+name: enter-minor-mode
#+begin_src emacs-lisp :noweb tangle
  (if (derived-mode-p 'org-mode)
      (progn
        (message "org-mode doesn't need org-link-minor-mode")
        (org-link-minor-mode -1)
        )
    «enter-minor-mode-body»
    )
#+end_src

* Turning on org-link highlighting
:PROPERTIES:
:noweb-ref: enter-minor-mode-body
:ID:       8d3990a4-ee3b-4276-9a6b-53665c095133
:END:

Add the font-lock specification:

#+begin_src emacs-lisp
  (font-lock-add-keywords nil org-link-minor-mode-keywords t)
#+end_src

Enable =org-toggle-link-display= for this buffer only by
making =org-descriptive-links= buffer local:

#+begin_src emacs-lisp
  (org-set-local 'org-descriptive-links 'org-descriptive-links)
  (if org-descriptive-links (add-to-invisibility-spec '(org-link)))
#+end_src

This is the magic that makes the link body appear if you backspace
into it (or use replace to make it no longer a link):

#+begin_src emacs-lisp
  (org-set-local 'font-lock-unfontify-region-function
                 'org-unfontify-region)
#+end_src

Finally, we refontify the buffer using org's own method:

#+begin_src emacs-lisp
  (org-restart-font-lock)
#+end_src

* Exiting the minor mode
:PROPERTIES:
:ID:       95c5162b-ec40-4bb7-849c-f10d12185b29
:END:

Again, we don't run this code if we're already in org-mode:

#+name: exit-minor-mode
#+begin_src emacs-lisp :noweb tangle
  (unless (derived-mode-p 'org-mode)
    (save-excursion
      (save-match-data
        «exit-minor-mode-body»
        )
      )
    )
#+end_src

To save the current buffer's state, we wrap the body of the function
in =save-excursion= and =save-match-data=.

* Exit minor mode body
:PROPERTIES:
:noweb-ref: exit-minor-mode-body
:ID:       a555c274-844a-4913-91e0-6be72a1911e1
:END:

Remove all org-link properties (without setting =buffer-modified-p=)
using org-mode's =org-unfontify-region=:

#+begin_src emacs-lisp
  (font-lock-remove-keywords nil org-link-minor-mode-keywords)
  (org-remove-from-invisibility-spec '(org-link))
  (goto-char (point-min))
  (while (re-search-forward org-link-minor-mode-link-regexp nil t)
    (with-silent-modifications
      (org-unfontify-region (match-beginning 0) (match-end 0))
      )
    )
#+end_src

Note: if we wanted to have more link types, we'd have to use the union
of the various regexps here.

Restore existing font lock highlighting

#+begin_src emacs-lisp
  (org-restart-font-lock)
#+end_src

* Provide
:PROPERTIES:
:ID:       317688ba-da16-4a42-9e4f-20b06a8d86cf
:END:

Finally, we add the =provide= feature clause.

#+name: provide
#+begin_src emacs-lisp
(provide 'org-link-minor-mode)
#+end_src

* Complete source

Here is the complete source:

#+name: source
#+begin_src emacs-lisp :tangle org-link-minor-mode.el :noweb yes :padline no
«header-comments»

«requires»

«define-minor-mode»

«provide»
#+end_src
