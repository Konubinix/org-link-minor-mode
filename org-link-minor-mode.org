:header:
#+OPTIONS: ^:{} <:nil todo:nil tags:nil num:nil H:4 toc:2
#+HTML_HEAD: <style>var { font-family: monospace; font-style: normal; }</style>
#+HTML_HEAD: <style>var.variable { font-style: italic; }</style>
#+HTML_HEAD: <style>var.keyword { font-weight: bold; }</style>
#+HTML_HEAD: <style>var.mode { font-weight: bold; }</style>
#+MACRO: arg   @@html:<var class="arg">$1</var>@@
#+MACRO: code  @@html:<code>$1</code>@@
#+MACRO: const @@html:<var class="const">$1</var>@@
#+MACRO: file  @@html:<var class="filename">$1</var>@@
#+MACRO: fn    @@html:<code class="function">$1</code>@@
#+MACRO: key   @@html:<kbd>$1</kbd>@@
#+MACRO: kw    @@html:<var class="keyword">$1</var>@@
#+MACRO: macro @@html:<var class="macro">$1</var>@@
#+MACRO: mode  @@html:<var class="mode">$1</var>@@
#+MACRO: param @@html:<var class="param">$1</var>@@
#+MACRO: prop  @@html:<var class="property">$1</var>@@
#+MACRO: var   @@html:<var class="variable">$1</var>@@
#+PROPERTY: header-args :eval never :noweb no-export
#+LINK: elisp http://www.gnu.org/software/emacs/manual/html_node/elisp/%s.html
:end:
#+TITLE:org-link-minor-mode
#+AUTHOR:Sean O'Halpin
#+DATE:2012-08-25
#+DATE:2020-01-24

* Header
:PROPERTIES:
:ID:       14f6023b-6e29-4266-82f2-d227902d4cf7
:header-args: :eval no-export
:END:

The header has the following structure:

#+name: header
#+begin_src emacs-lisp
;;; <<title-line>>
;;
;; <<copyright>>
;;
;; <<package-info>>
;;
;; <<gnu-std-header>>
;;
;;; Commentary:
;;
;; <<commentary>>
;;
;;; Code:
#+end_src

** Title line

The title line is the first line in the file and shows the name of the
file and a short description. It also turns on
{{{var(lexical-binding)}}}.

#+name: title-line
#+begin_src org
org-link-minor-mode.el --- Enable org-mode links in non-org modes -*- lexical-binding: t; -*-
#+end_src

** Metadata
*** Copyright

#+name: copyright
#+begin_src org
Copyright (C) 2012-2020
#+end_src

*** Email

#+name: email
#+begin_src org
Sean O'Halpin <sean.ohalpin@gmail.com>
#+end_src

*** Dates

**** Created
#+name: created
#+begin_src org
20120825
#+end_src

**** Modified
#+name: modified
#+begin_src emacs-lisp :eval yes
(format-time-string "%Y%m%d")
#+end_src

**** Version

#+name: version
#+begin_src org
0.0.3
#+end_src

**** Package-Version
#+name: package-version
#+begin_src emacs-lisp :eval yes
(format-time-string "%Y%m%d.%H%M")
#+end_src

*** Package info

Specify the package metadata and that it requires at least Org 8 and
Emacs 24.3 (for =lexical-binding= and =setq-local=):

#+name: package-info
#+begin_src org
Author: <<email>>
Maintainer: <<email>>
Created: <<created>>
Modified: <<modified()>>
Version: <<version>>
Package-Requires: ((emacs "24.3"))
Package-Version: <<package-version()>>
Keywords: hypermedia
Url: https://github.com/seanohalpin/org-link-minor-mode

Changes for org v9: Stefan-W. Hahn <stefan dot hahn at s-hahn dot de>
#+end_src

** GNU GPL 3 comment

All files submitted to ELPA should contain the standard GNU GPL 3
comment:

#+name: gnu-std-header
#+begin_src org
This file is free software; you can redistribute it and/or modify
it under the terms of the GNU General Public License as published by
the Free Software Foundation; either version 3, or (at your option)
any later version.

This file is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU General Public License for more details.

You should have received a copy of the GNU General Public License
along with this program.  If not, see <http://www.gnu.org/licenses/>.
#+end_src

** Commentary

A brief explanation of the package:

#+name: commentary
#+begin_src org
Enables org-mode links of the form:

  http://www.bbc.co.uk
  man:emacs
  info:emacs
  <http://www.bbc.co.uk>
  [[http://www.bbc.co.uk][BBC]]
  [[org-link-minor-mode]]
  [2012-08-18]
  <2012-08-18>

Note that `org-toggle-link-display' will also work when this mode
is enabled.
#+end_src

* Code

** Require org-mode
:PROPERTIES:
:ID:       0e3b4960-cc9f-4ebe-9acf-c6632b3c68f2
:END:

As we rely on many {{{mode(org-mode)}}} functions, to avoid warnings, we
load {{{mode(org-mode)}}}:

#+name: requires
#+begin_src emacs-lisp
(require 'org)
#+end_src

** Declarations for byte-code compiler

#+name: declarations-for-byte-code
#+begin_src emacs-lisp
;; Following declarations are necessary to make the byte compiler happy.

;; For org v8 compatibility (if used with org v9)
(declare-function org-activate-plain-links "org" (limit))
(declare-function org-activate-angle-links "org" (limit))
(declare-function org-activate-bracket-links "org" (limit))
(declare-function org-decompose-region "org-compat" (beg end))

;; For org v9 compatibility (if used with org v8)
(declare-function org-activate-links "org" (limit))
(declare-function org-activate-dates "org" (limit))
#+end_src

** Using the {{{macro(define-minor-mode)}}} macro
:PROPERTIES:
:ID:       2e643e2a-acb4-43dd-92b3-d8048f66f854
:END:

The simplest way to define a new minor mode is to use the
[[elisp:Defining-Minor-Modes][{{{fn(define-minor-mode)}}}]] macro:

#+name: define-minor-mode
#+begin_src emacs-lisp
;;;###autoload
(define-minor-mode org-link-minor-mode
  "Toggle display of org-mode style links in non-org-mode buffers."
  :lighter " org-link"
  :keymap org-link-minor-mode-map
  <<body>>)
#+end_src

The [[http://www.gnu.org/software/emacs/manual/html_node/elisp/Autoload.html][{{{kw(autoload)}}} magic comment]] is used by emacs at build time to add the
{{{fn(org-link-minor-mode)}}} function to {{{file(loaddefs.el)}}}.

The {{{kw(:lighter)}}} keyword parameter defines what appears in the mode line.

** The body
:PROPERTIES:
:ID:       4e5b0abe-bc41-43f6-9271-b6365a7b5bce
:END:

#+name: body
#+begin_src emacs-lisp
(let ((lk org-highlight-links)
      org-link-minor-mode-keywords)
  <<set-up-font-lock-keywords>>
  <<enter-exit-mode>>)
#+end_src
# NB: Indent is hack to fix indentation in tangled output file

We start the body by setting up the font lock keywords in the variable
{{{var(org-link-minor-mode-keywords)}}}, using org-mode's own {{{fn(org-activate-xxx)}}}
functions to do the heavy lifting.

Note: we do this /before/ entering the mode so the definition of
{{{var(org-link-minor-mode-keywords)}}} is available for when we want
to leave the mode.

#+begin_comment
The default value for {{{var(org-highlight-links)}}} is:

: (bracket angle plain radio tag date footnote)

Help for this variable in the Customize buffer says:

#+begin_quote
If you set this variable during an Emacs session, use ‘org-mode-restart’
in the Org buffer so that the change takes effect.
#+end_quote
#+end_comment

#+name: set-up-font-lock-keywords
#+begin_src emacs-lisp
(if (fboundp 'org-activate-links)
    ;; from Org v9.2
    (setq org-link-minor-mode-keywords
          (list
           '(org-activate-links)
           (when (memq 'tag lk) '(org-activate-tags (1 'org-tag prepend)))
           (when (memq 'radio lk) '(org-activate-target-links (1 'org-link t)))
           (when (memq 'date lk) '(org-activate-dates (0 'org-date t)))
           (when (memq 'footnote lk) '(org-activate-footnote-links))))
  (setq org-link-minor-mode-keywords
        (list
         (when (memq 'tag lk) '(org-activate-tags (1 'org-tag prepend)))
         (when (memq 'angle lk) '(org-activate-angle-links (0 'org-link t)))
         (when (memq 'plain lk) '(org-activate-plain-links (0 'org-link t)))
         (when (memq 'bracket lk) '(org-activate-bracket-links (0 'org-link t)))
         (when (memq 'radio lk) '(org-activate-target-links (0 'org-link t)))
         (when (memq 'date lk) '(org-activate-dates (0 'org-date t)))
         (when (memq 'footnote lk) '(org-activate-footnote-links)))))
#+end_src

We then branch depending on whether we're entering or exiting the
mode:

#+name: enter-exit-mode
#+begin_src emacs-lisp
(if org-link-minor-mode
    <<enter-minor-mode>>
  <<exit-minor-mode>>)
#+end_src

** Entering the minor mode
:PROPERTIES:
:ID:       a1b76a00-4444-4b5c-bbfa-54c88dac769f
:END:

If we're already in {{{mode(org-mode)}}}, display a message and switch
{{{mode(org-link-minor-mode)}}} off. We need to do it this way as by this point
we've already entered the minor mode ({{{mode(org-link-minor-mode)}}}
is {{{const(t))}}}:

#+name: enter-minor-mode
#+begin_src emacs-lisp
(if (derived-mode-p 'org-mode)
    (progn
      (message "org-mode doesn't need org-link-minor-mode")
      (org-link-minor-mode -1))
  <<enter-minor-mode-body>>)
#+end_src

** Minor mode body

When we enter the minor mode, we
1. add the font lock keywords
2. define the keymap that will be active within links
3. set the unfontify function
4. set up {{{var(org-descriptive-links)}}}
5. turn on link display

#+name: enter-minor-mode-body
#+begin_src emacs-lisp :exports none
<<font-lock-spec>>
<<define-keymap>>
<<unfontify-region-function>>
<<org-descriptive-links>>
<<turn-on-link-display>>
#+end_src

** Turning on org-link highlighting
:PROPERTIES:
:noweb-ref: enter-minor-mode-body
:ID:       8d3990a4-ee3b-4276-9a6b-53665c095133
:END:

Add the font-lock specification:

#+name: font-lock-spec
#+begin_src emacs-lisp
(font-lock-add-keywords nil org-link-minor-mode-keywords t)
#+end_src

Org mode associates the {{{var(org-mouse-map)}}} keymap with
links. This becomes active when the mouse or point is over a link.

We can use this to enable {{{key(return)}}} to follow link (and
{{{key(tab)}}} to next link, {{{key(backtab)}}} to previous, etc.):

#+name: define-keymap
#+begin_src emacs-lisp
(kill-local-variable 'org-mouse-map)
(setq-local org-mouse-map
            (let ((map (make-sparse-keymap)))
              (define-key map [return] 'org-open-at-point)
              (define-key map [tab] 'org-next-link)
              (define-key map [backtab] 'org-previous-link)
              (define-key map [mouse-2] 'org-open-at-point)
              (define-key map [follow-link] 'mouse-face)
              map))
#+end_src

Reusing {{{var(org-mouse-map)}}} like this is a hack.  This keymap is set as
a text property of links in {{{fn(org-activate-links)}}}, etc. so it's
simpler to co-opt it than to replace those functions.

Make {{{var(org-descriptive-links)}}} local, turning it off.  We will switch
descriptive links on when we call {{{fn(org-toggle-link-display)}}} below.

#+name: org-descriptive-links
#+begin_src emacs-lisp
(setq-local org-descriptive-links nil)
#+end_src

This is the magic that makes the link body appear if you backspace
into it (or use replace to make it no longer a link):
#+name: unfontify-region-function
#+begin_src emacs-lisp
(setq-local font-lock-unfontify-region-function
            'org-link-minor-mode--unfontify-region)
#+end_src

The [[elisp:Search_002dbased-Fontification][documentation]] for [[elisp:Other-Font-Lock-Variables][{{{fn(font-lock-unfontify-region-function)}}}]] is a bit
sparse but reading {{{fn(org-unfontify-region)}}} at least shows you what it
should do.

Finally, we refontify the buffer using
{{{fn(org-toggle-link-display)}}}. This will reset
{{{var(org-descriptive-links)}}} to {{{const(t)}}}.

#+name: turn-on-link-display-orig
#+begin_src emacs-lisp :exports none :tangle no
(org-restart-font-lock)
#+end_src

#+name: turn-on-link-display
#+begin_src emacs-lisp
(org-toggle-link-display)
#+end_src

** Exiting the minor mode
:PROPERTIES:
:ID:       95c5162b-ec40-4bb7-849c-f10d12185b29
:END:

Again, we don't run this code if we're already in org-mode:

#+name: exit-minor-mode
#+begin_src emacs-lisp
(unless (derived-mode-p 'org-mode)
  <<exit-minor-mode-body>>)
#+end_src

Remove all org-link font-lock properties:

#+name: remove-keywords
#+begin_src emacs-lisp
(font-lock-remove-keywords nil org-link-minor-mode-keywords)
#+end_src

Turn off link display:

#+name: turn-off-link-display
#+begin_src emacs-lisp
(setq org-descriptive-links t)
(org-toggle-link-display)
#+end_src

Kill the local variables:

#+name: kill-local-variables
#+begin_src emacs-lisp
(kill-local-variable 'org-descriptive-links)
(kill-local-variable 'org-mouse-map)
(kill-local-variable 'font-lock-unfontify-region-function)
#+end_src

#+name: exit-minor-mode-body
#+begin_src emacs-lisp :noweb no-export :exports none
<<remove-keywords>>
<<turn-off-link-display>>
<<kill-local-variables>>
#+end_src

*** Remove text properties

{{{fn(org-unfontify-region)}}} does not remove the {{{prop(help-echo)}}},
{{{prop(htmlize-link)}}} and {{{prop(rear-nonsticky)}}} properties, so I've copied
that function and added those in.

Note: This looks like a bug in {{{fn(org-unfontify-region)}}}.

We can ignore the {{{prop(fontified)}}} property as that belongs to
{{{mode(font-lock-mode)}}}.

#+name: org-link-minor-mode--unfontify-region
#+begin_src emacs-lisp
(defun org-link-minor-mode--unfontify-region (beg end)
  "Remove org-link fontification between BEG and END."
  (font-lock-default-unfontify-region beg end)
  (let* ((buffer-undo-list t)
         (inhibit-read-only t) (inhibit-point-motion-hooks t)
         (inhibit-modification-hooks t)
         deactivate-mark buffer-file-name buffer-file-truename)
    (if (fboundp 'org-decompose-region)
        (org-decompose-region beg end)
      (decompose-region beg end))
    (remove-text-properties beg end
                            '(mouse-face t keymap t org-linked-text t
                                         invisible t intangible t
                                         help-echo t rear-nonsticky t
                                         htmlize-link t
                                         org-no-flyspell t org-emphasis t))
    (org-remove-font-lock-display-properties beg end)))
#+end_src

Note that =org-link-minor-mode-unfontify-region= is obsolete.

#+begin_src emacs-lisp
(defalias 'org-link-minor-mode-unfontify-region 'org-link-minor-mode--unfontify-region)
(make-obsolete 'org-link-minor-mode-unfontify-region 'org-link-minor-mode--unfontify-region
               "org-link-minor-mode 0.0.3")
#+end_src

The keymap is defined in case you want to define your own key bindings
for this mode. It is not used by {{{mode(org-link-minor-mode)}}} itself.

#+name: keymap
#+begin_src emacs-lisp
(defvar org-link-minor-mode-map (make-sparse-keymap)
  "Local keymap.")
(make-variable-buffer-local 'org-link-minor-mode-map)
#+end_src

** Provide
:PROPERTIES:
:ID:       317688ba-da16-4a42-9e4f-20b06a8d86cf
:END:

Finally, we add the {{{fn(provide)}}} feature clause so that we can later
{{{code((require 'org-link-minor-mode))}}}.

#+name: provide
#+begin_src emacs-lisp
(provide 'org-link-minor-mode)
#+end_src

* Complete source

Here is the complete source:

#+name: source
#+begin_src emacs-lisp :tangle org-link-minor-mode.el :tangle-mode (identity #o444) :noweb yes :padline no
<<header>>

<<requires>>

<<declarations-for-byte-code>>

<<org-link-minor-mode--unfontify-region>>

<<keymap>>

<<define-minor-mode>>

<<provide>>
;;; org-link-minor-mode.el ends here
#+end_src
